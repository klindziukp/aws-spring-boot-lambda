import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group 'com.klindziuk.aws'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.2")
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootBuildImage {
    enabled = false
}

bootJar {
    enabled = false
}

shadowJar {
    archiveClassifier.set('')
    dependencies {
        exclude(
                dependency("org.springframework.cloud:spring-cloud-function-web:4.1.3"))
    }
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    append 'META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
    append 'META-INF/spring/org.springframework.boot.actuate.autoconfigure.web.ManagementContextConfiguration.imports'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = "append"
    }
        manifest {
        attributes(
                'Main-Class': 'com.klindziuk.aws.handler.DynamodbLambdaFunctionApplication'
        )
    }
}

dependencies {
    implementation project(':payment-model')
    implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws:4.1.3'

    implementation 'io.awspring.cloud:spring-cloud-aws-core:3.1.1'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter:3.1.1'
    implementation 'io.awspring.cloud:spring-cloud-aws-autoconfigure:3.1.1'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-sns:3.1.1'

    implementation 'com.amazonaws:aws-lambda-java-events:3.11.4'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.3'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}
